apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        // versionCode lesen und um 1 erhöhen
        def verCode = versionProps['VERSION_CODE'].toInteger() + 1
        versionProps['VERSION_CODE'] = verCode.toString()
        // versionName lesen und um 1 erhöhen
        def verName = versionProps['VERSION_NAME_LAST'].toInteger() + 1
        versionProps['VERSION_NAME_LAST'] = verName.toString()
        // wieder speichern
        versionProps.store(versionPropsFile.newWriter(), null)

        // hier UND in version.properties ändern bei Major Updates
        verName = "1.0." + verName.toString()

        defaultConfig {
            applicationId "com.medeozz.wikimap"
            minSdkVersion 14
            targetSdkVersion 22
            versionCode verCode
            versionName verName
        }

    } else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'ch.acra:acra:4.6.1'
    compile 'com.afollestad:material-dialogs:0.7.3.2'
    compile 'com.google.android.gms:play-services:7.3.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.google.maps.android:android-maps-utils:0.3.4'
    compile 'com.github.navasmdc:MaterialDesign:1.+@aar'
    compile 'it.neokree:MaterialNavigationDrawer:1.3.3'
    compile 'com.android.support:cardview-v7:22.0.0'
    compile 'com.android.support:recyclerview-v7:22.0.0'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.nispok:snackbar:2.10.7'
}
